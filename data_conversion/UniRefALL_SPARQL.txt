PREFIX taxon: <http://purl.uniprot.org/taxonomy/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX up: <http://purl.uniprot.org/core/>

SELECT DISTINCT
  (substr(str(?cluster50), 32) AS ?cluster50id)
  (substr(str(?cluster90), 32) AS ?cluster90id)
  (substr(str(?cluster100), 32) AS ?cluster100id)
  (substr(str(?kingdom), 34) AS ?kingdomid)
  ?sequence
WHERE
{
  VALUES ?kingdom { taxon:2 taxon:2759 }
  ?organism rdfs:subClassOf ?kingdom .

  ?protein a up:Protein ;
           up:organism ?organism .

  ?sequenceClass a up:Sequence ;
                 rdf:value ?sequence ;
                 up:memberOf ?cluster100 ;
                 up:memberOf ?cluster50 ;
                 up:memberOf ?cluster90 ;
                 up:sequenceFor ?protein .

  ?cluster100 up:identity 1.0 .
  ?cluster50 up:identity 0.5 .
  ?cluster90 up:identity ?identity90 .
  FILTER ( ?identity90 != 1.0 && ?identity90 != 0.5 )

  OPTIONAL {
    ?protein up:annotation ?annotation .
    {
      ?annotation a up:Non-adjacent_Residues_Annotation .
    }  UNION {
      ?annotation a up:Non-terminal_Residue_Annotation
      }
  }
  FILTER(! BOUND(?annotation))
}
